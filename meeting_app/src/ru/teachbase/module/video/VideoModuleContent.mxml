<?xml version="1.0" encoding="utf-8"?>
<module:ModuleInstance xmlns:fx="http://ns.adobe.com/mxml/2009"
                       xmlns:s="library://ns.adobe.com/flex/spark"
                       xmlns:components="ru.teachbase.components.*"
                       xmlns:layout="ru.teachbase.layout.*" xmlns:module="ru.teachbase.components.module.*"
                       implements="ru.teachbase.utils.interfaces.ILocalable"
                       creationComplete="creationCompleteHandler(event)" addedToStage="addedToStage(event)">
    <fx:Script>
		<![CDATA[
        import flash.net.NetStream;

        import mx.events.CollectionEvent;

        import mx.events.FlexEvent;

        import ru.teachbase.manage.streams.model.NetStreamClient;

        import ru.teachbase.manage.streams.model.NetStreamClient;
        import ru.teachbase.manage.streams.model.StreamType;

        import ru.teachbase.model.App;
        import ru.teachbase.module.video.components.IVideoContainer;
        import ru.teachbase.module.video.components.VideoContainer;
        import ru.teachbase.utils.Localizer;
        import ru.teachbase.utils.Permissions;
        import ru.teachbase.utils.shortcuts.style;
        import ru.teachbase.utils.shortcuts.translate;

        private var elements:Vector.<IVideoContainer> = new Vector.<IVideoContainer>;

        [Bindable]
        private var _camVideoContainerInstance:IVideoContainer;

        [Bindable]
        private var _streams:int;

        private var _streamsDic:Dictionary = new Dictionary(true);

        private function addedToStage(evt:Event):void {
            App.publisher.enable();
        }

        private function creationCompleteHandler(event:FlexEvent):void {

            label = translate('label', 'video');

            Localizer.addItem(this);

            for each(var ns:NetStream in App.meeting.streamList.toArray().filter(filter))
                addVideo(ns,(ns.client as NetStreamClient).data.sid);

            App.meeting.streamList.addEventListener(CollectionEvent.COLLECTION_CHANGE, onStreamChangeEvent);

        }

        private function onStreamChangeEvent(evt:CollectionEvent):void{

            //TODO:

        }


        private function filter(stream:NetStream, index:int = 0, arr:Array = null):Boolean {
            return Boolean(stream && (stream.client as NetStreamClient) && (stream.client as NetStreamClient).data.type == StreamType.VIDEO && (!(_streamsDic[stream])));
        }

        private function getVideoContainer(userId:Number):IVideoContainer {
            for each (var vc:IVideoContainer in elements) {
                if (vc.from == userId) {
                    return vc;
                }
            }
            return null;
        }


        private function addVideo(ns:NetStream = null, userId:Number = 0):void {
            var vc:VideoContainer = new VideoContainer();

            vc.stream = ns;
            vc.from = userId;
            vc.isAdmin = Permissions.isAdmin(permission);
            vc.addEventListener("removeStream", onRemoveLocalStream);
            vc.addEventListener("removeUserStream", onRemoveUserStream);// убивает поток
            vc.addEventListener("removeVideo", onRemoveLocalVideo); // при переключении в аудио режим, не убивает поток
            elements.push(vc);
            videoCont.addElement(vc);
            _streams++;
        }

        private function addCamera(cam:Camera):void {

            if (!_camVideoContainerInstance) {
                _camVideoContainerInstance = new VideoContainer();
                _camVideoContainerInstance.isAdmin = true;
                _camVideoContainerInstance.addEventListener("removeStream", onRemoveLocalStream);
                elements.push(_camVideoContainerInstance);
                _camVideoContainerInstance.camera = cam;
                videoCont.addElement(_camVideoContainerInstance);
            } else {
                _camVideoContainerInstance.camera = cam;
            }
        }

        private function removeVideo(vc:IVideoContainer):void {
            elements.splice(elements.indexOf(vc), 1);
            if (vc.stage)
                videoCont.removeElement(vc);
            vc = null;
            _streams--;
        }

        private function removeCamera():void {
            if (_camVideoContainerInstance) {
                elements.splice(elements.indexOf(_camVideoContainerInstance), 1);
                videoCont.removeElement(_camVideoContainerInstance);
                _camVideoContainerInstance.camera = null;
                _camVideoContainerInstance = null;
            }
        }

        private function onRemoveLocalStream(event:Event):void {
            const vc:IVideoContainer = event.target as IVideoContainer

            removeVideoContainer(vc);
        }


        private function onRemoveUserStream(event:Event):void {
            const vc:IVideoContainer = event.target as IVideoContainer
            App.streams.closeUserStream(vc.from);
            removeVideoContainer(vc);
        }

        private function onRemoveLocalVideo(event:Event):void {
            const vc:IVideoContainer = event.target as IVideoContainer;
            removeVideoContainer(vc);
        }

        private function removeVideoContainer(vc:IVideoContainer):void {
            if (!vc.camera) {
                removeVideo(vc as IVideoContainer);
            } else {
                App.publisher.closeCamera();
            }
        }

        override public function hide():void {
            super.hide();
            App.publisher.disable();

            for each(var el:IVideoContainer in elements) {
                removeVideoContainer(el);
            }
        }


        public function localize():void {
            label = translate('label', 'video');
            bottomButton && (bottomButton.label = translate('turn_on_cam', 'settings'));
            bigBlbl && (bigBlbl.text = translate('turn_on_cam', 'settings'));
        }


        override public function set permission(value:int):void {
            p_enabled = Permissions.camAvailable(value);

            for each(var el:IVideoContainer in elements)
                el.isAdmin = Permissions.isAdmin(value);


            super.permission = value;
        }

        ]]>
	</fx:Script>

    <s:Group id="videoCont" width="100%" height="100%">
        <s:layout>
            <layout:VideoLayout ratio="0.75"/>
        </s:layout>
    </s:Group>
    <s:VGroup horizontalCenter="0" verticalCenter="0"
              visible="{_camVideoContainerInstance == null &amp;&amp; _streams == 0 &amp;&amp; p_enabled}">
        <components:CustomSkinableButton id="bigButton"
                                         initialize="with(bigButton){iconUp=style('video','bigCameraButton');iconDown=style('video','bigCameraButtonDown');iconOver=style('video','bigCameraButtonHover');}"
                                         skinClass="ru.teachbase.skins.CustomSkiningButtonSkin" focusSkin="{null}"
                                         click="App.publisher.toggleStartCamera()">
        </components:CustomSkinableButton>
        <s:Label id="bigBlbl" initialize="bigBlbl.text=translate('turn_on_cam','settings')" textAlign="center"
                 width="100%" color="0x8196A6"/>
    </s:VGroup>
    <s:Group width="100%" bottom="0" left="0" right="0"
             visible="{_camVideoContainerInstance == null &amp;&amp; _streams > 0 &amp;&amp; p_enabled}"
             show="videoCont.bottom = 46" hide="videoCont.bottom = 0">
        <s:layout>
            <s:BasicLayout/>
        </s:layout>
        <s:Image source="{style('modulecontainer','bottomBackground')}" top="0" left="0" right="0" bottom="0"
                 scaleMode="stretch" height="46"/>
        <components:CustomSkinableButton horizontalCenter="0" bottom="6" id="bottomButton"
                                         initialize="with(bottomButton){label=translate('turn_on_cam','settings');iconUp=style('video','bottomCameraButton');iconDown=style('video','bottomCameraButtonDown');iconOver=style('video','bottomCameraButtonHover');}"
                                         skinClass="ru.teachbase.skins.StandartButtonWithIcon"
                                         click="App.publisher.toggleStartCamera()">
        </components:CustomSkinableButton>
    </s:Group>
</module:ModuleInstance>
