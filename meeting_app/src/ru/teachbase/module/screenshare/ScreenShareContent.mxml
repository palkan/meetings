<?xml version="1.0" encoding="utf-8"?>
<module:ModuleInstance xmlns:fx="http://ns.adobe.com/mxml/2009"
                       xmlns:s="library://ns.adobe.com/flex/spark"
                       xmlns:components="ru.teachbase.components.*" xmlns:module="ru.teachbase.components.module.*"
                       implements="ru.teachbase.utils.interfaces.ILocalable"
                       creationComplete="creationCompleteHandler(event)">
    <module:states>
        <s:State id="sharing" name="sharing"/>
        <s:State id="iams" name="iamsharing"/>
        <s:State id="iamp" name="iampreparing"/>
        <s:State id="prepare" name="prepare"/>
        <s:State id="idle" name="idle"/>
    </module:states>
    <fx:Script>
		<![CDATA[
        import mx.events.CollectionEvent;
        import mx.events.CollectionEventKind;
        import mx.events.FlexEvent;
        import mx.rpc.Responder;

        import ru.teachbase.constants.PacketType;
        import ru.teachbase.events.GlobalEvent;
        import ru.teachbase.manage.rtmp.RTMPListener;
        import ru.teachbase.manage.rtmp.events.RTMPEvent;
        import ru.teachbase.manage.streams.model.NetStreamClient;
        import ru.teachbase.manage.streams.model.StreamType;
        import ru.teachbase.model.App;
        import ru.teachbase.model.User;
        import ru.teachbase.module.screenshare.components.VideoContainerShare;
        import ru.teachbase.utils.BrowserUtils;
        import ru.teachbase.utils.Localizer;
        import ru.teachbase.utils.Permissions;
        import ru.teachbase.utils.shortcuts.*;

        private const listener:RTMPListener = new RTMPListener(PacketType.SCREEN_SHARING);

        private var _activeUser:User;

        private const user:User = App.user;

        private var _netStream:NetStream;

        private var _vc:VideoContainerShare;

        private var _sizeInited:Boolean = false;

        private var _sizeId:uint;

        [Bindable]
        private var _p_enabled:Boolean = false;


        override public function init():void{
            listener.initialize();
            listener.addEventListener(RTMPEvent.DATA, handleMessage);
        }


        private function creationCompleteHandler(event:FlexEvent):void {

            currentState = idle.name;

            label = translate('label', 'screenshare');

            Localizer.addItem(this);

            _p_enabled = Permissions.docsAvailable(App.user.permissions);

            App.meeting.streamList.addEventListener(CollectionEvent.COLLECTION_CHANGE, onStreamChangeEvent);

            GlobalEvent.addEventListener(GlobalEvent.RECONNECT, handleReconnect);
            GlobalEvent.addEventListener(GlobalEvent.RESET, handleReset);
            GlobalEvent.addEventListener(GlobalEvent.USER_LEAVE, handleUserLeave);

            start && (start.enabled = start.visible = _p_enabled);

            var share_streams:Array = App.meeting.streamList.toArray().filter(filter);

            if (share_streams.length) {

                const ns:NetStream = share_streams[0];

                _activeUser = App.meeting.usersByID[(ns.client as NetStreamClient).data.user_id];

                addStream(ns);

                listener.readyToReceive = true;
                _initialized = true;
            } else
                rtmp_history(PacketType.SCREEN_SHARING, new mx.rpc.Responder(handleHistory, $null));
        }


        private function handleUserLeave(e:GlobalEvent):void{
            if(_activeUser && _activeUser.sid == e.value.sid) dispose();
        }


        private function handleHistory(data:Object):void {

            data && processData(data);

            listener.readyToReceive = true;

            _initialized = true;

        }


        private function handleReset(e:GlobalEvent):void{

            dispose();

            listener.dispose();
            listener.initialize();


            App.meeting.streamList.removeEventListener(CollectionEvent.COLLECTION_CHANGE, onStreamChangeEvent);

        }


        private function handleReconnect(e:GlobalEvent):void {

            dispose();

            listener.dispose();
            listener.initialize();

            _p_enabled = Permissions.docsAvailable(App.user.permissions);

            App.meeting.streamList.addEventListener(CollectionEvent.COLLECTION_CHANGE, onStreamChangeEvent);

            var share_streams:Array = App.meeting.streamList.toArray().filter(filter);

            if (share_streams.length) {

                //todo: dry this!
                const ns:NetStream = share_streams[0];

                _activeUser = App.meeting.usersByID[(ns.client as NetStreamClient).data.user_id];

                addStream(ns);
                listener.readyToReceive = true;
                _initialized = true;
            } else
                rtmp_history(PacketType.SCREEN_SHARING, new mx.rpc.Responder(handleHistory, $null));


        }

        private function onStreamChangeEvent(e:CollectionEvent):void {

            switch (e.kind) {
                case CollectionEventKind.REMOVE:
                {
                    for each(var ns:NetStream in e.items) {
                        (_netStream === ns) && dispose();
                    }
                    break;
                }
                case CollectionEventKind.ADD:
                {
                    var share_streams:Array = e.items.filter(filter);

                    share_streams.length && addStream(share_streams[0]);
                    break;
                }
                case CollectionEventKind.UPDATE:
                {
                    break;
                }
                case CollectionEventKind.RESET:
                {
                    dispose();
                    break;
                }
            }
        }


        private function filter(stream:NetStream, index:int = 0, arr:Array = null):Boolean {
            return Boolean(stream && (stream.client as NetStreamClient) && (stream.client as NetStreamClient).data.type == StreamType.SCREEN);
        }


        private function handleMessage(e:RTMPEvent):void {

            const data:Object = e.packet.data;

            processData(data);
        }

        private function processData(data:Object):void {

            if(data && data.type === "prepare" && data.uid) {
                prepareShare(data.uid);
            } else if (data && data.type === "stop") {
                dispose();
            }else if(data && data.type === "start" && data.uid == user.sid){
                currentState = iams.name;
            }

        }


        private function prepareShare(uid:Number):void {

            _activeUser = App.meeting.usersByID[uid];

            if (!_activeUser) return;

            if (user.sid === uid) {

                currentState = iamp.name;

                statusText.text = translate("prepare_me", "screenshare");

            } else {

                currentState = prepare.name;

                statusText.text = translate("prepare", "screenshare", _activeUser.fullName);

            }

        }


        private function startShare():void {

            BrowserUtils.sendCall("exCall","run",config("net/rtmp"),config("net/rtmp_port"),App.meeting.id,App.user.sid);

            debug("Share:",config("net/rtmp"),config("net/rtmp_port"),App.meeting.id,App.user.sid);

            rtmp_send(PacketType.SCREEN_SHARING, {type: "prepare", uid: App.user.sid});

        }

        private function stopShare():void {

            BrowserUtils.sendCall("exCall","stop");

            rtmp_send(PacketType.SCREEN_SHARING, {type: "stop", uid: App.user.sid});

        }


        private function addStream(stream:NetStream):void {

            if (_netStream) {
                warning("Stream already exists: " + (_netStream.client as NetStreamClient).data.name);
                return;
            }

            _netStream = stream;

            if(!_activeUser){

                if((_netStream.client as NetStreamClient) && (_netStream.client as NetStreamClient).data)
                    _activeUser = App.meeting.usersByID[(_netStream.client as NetStreamClient).data.user_id];


                if(!_activeUser){
                    warning("No active user!");
                    return;
                }
            }

            label = translate('label', 'screenshare') + " — " + _activeUser.fullName;


            _netStream.client.onMouseData = function (metadata:Object):void {
                if (!metadata)
                    return;

                if (metadata.msgtype && metadata.msgtype == "mouse") {
                    _vc && _vc.toggleCursor(new Point(metadata.posx, metadata.posy));
                }

            };

            _sizeId = setInterval(setVideoSize,100);

            currentState = sharing.name;

            _vc = new VideoContainerShare();

            _vc.stream = _netStream;
            _vc.from = _activeUser.sid;

            _vc.percentHeight = 100;
            _vc.percentWidth = 100;

            addElement(_vc);
        }

        override public function hide():void {
            super.hide();
            dispose();
        }

        public function dispose():void {

            _netStream = null;

            if (_vc) {
                _vc.dispose();
                this.containsElement(_vc) && removeElement(_vc);
                _vc = null;
            }

            _sizeInited = false;

            statusText && (statusText.text = "");

            _activeUser = null;
            label = translate('label', 'screenshare');

            currentState = idle.name;
        }

        public function localize():void {

            if (!_activeUser)
                label = translate('label', 'screenshare');
            else
                label = translate('label', 'screenshare') + " — " + _activeUser.fullName;

            start && (start.label = translate('start', 'screenshare'));
            stopshareBtn && (stopshareBtn.label = translate('stop_share', 'screenshare'));
            descTxt && (descTxt.text = translate('desc', 'screenshare'));
        }



        private function setVideoSize():void{

            if (!_sizeInited && _vc && _vc.width > 0 && _vc.videoWidth > 0) {
                _vc && _vc.setActualSize(_vc.width, _vc.height);
                _sizeInited = true;
                clearInterval(_sizeId);
            }

        }


        override public function set permissions(value:uint):void {

            super.permissions = value;

            _p_enabled = Permissions.docsAvailable(value);

            if(!_p_enabled && _activeUser && (_activeUser.sid === user.sid)) dispose();

        }
        ]]>
	</fx:Script>
    <s:Label id="statusText" horizontalCenter="0" excludeFrom="idle,sharing" verticalCenter="-60"/>

    <s:VGroup verticalCenter="0" horizontalCenter="0" includeIn="prepare,iampreparing">
        <s:BusyIndicator rotationInterval="50" symbolColor="0x71a7cd" width="60" height="60"/>
    </s:VGroup>

    <s:Group id="idleGroup" width="400" height="260" horizontalCenter="0" verticalCenter="0" includeIn="idle"
             visible="{_p_enabled}">
        <s:VGroup horizontalCenter="0" top="50" id="appGroup">
            <s:Group horizontalCenter="0" width="320">

                <components:CustomSkinableButton id="start" width="270" click="startShare()"
                                                 horizontalCenter="0"
                                                 skinClass="ru.teachbase.skins.StandartButtonWithIcon"
                                                 initialize="with(start){label=translate('start','screenshare');iconUp=style('screenshare','buttonIcon');iconDown=style('screenshare','buttonIcon');iconOver=style('screenshare','buttonIcon');visible = _p_enabled; enabled = _p_enabled;}"
                        />
                <s:RichText id="descTxt" initialize="with(descTxt){text = translate('desc','screenshare')}" width="100%"
                            y="{start.y + start.height + 22}" textAlign="center" fontSize="13"/>

            </s:Group>
        </s:VGroup>
    </s:Group>
    <s:Image source="{style('modulecontainer','bottomBackground')}" left="0" bottom="0" scaleMode="stretch" height="46"
             includeIn="iamsharing,iampreparing" width="100%"/>
    <s:Button includeIn="iamsharing,iampreparing" fontSize="13" id="stopshareBtn" width="214"
              initialize="with(stopshareBtn){label = translate('stop_share','screenshare');}" fontWeight="normal"
              left="7" bottom="7" click="stopShare()"
              skinClass="ru.teachbase.module.screenshare.skins.StopShareButtonSkin"/>


</module:ModuleInstance>
