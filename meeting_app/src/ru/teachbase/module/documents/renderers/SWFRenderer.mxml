<?xml version="1.0" encoding="utf-8"?>
<renderers:DefaultRenderer xmlns:fx="http://ns.adobe.com/mxml/2009"
                           xmlns:renderers="ru.teachbase.module.documents.renderers.*"
                           width="100%" height="100%">
    <fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
        import mx.collections.ArrayList;
        import mx.core.UIComponent;
        import mx.events.FlexEvent;

        import ru.teachbase.module.documents.components.SlideItem;
        import ru.teachbase.module.documents.model.FileItem;
        import ru.teachbase.module.documents.model.SlideAsset;

        protected var _slideItem:SlideItem;

        [Bindable]
        protected var _assetsList:ArrayList = new ArrayList();

        private var _slidesList:ArrayList = new ArrayList();

        private var _getAllSlides:Boolean = false;

        override protected function completeHandler(event:FlexEvent):void {
            _useWB = true;
            _ratio = -1;
        }

        public function goToSlide(value:int):void {
            slideItem = value;
        }


        public function preload(id:int):void {
            if (id < _slidesList.length) {
                const nextSlide:SlideItem = _slidesList.getItemAt(id) as SlideItem;
                nextSlide.load();
            }
        }


        override public function set file(value:FileItem):void {

            if (!value) return;

            const size:int = value.thumbs.length;

            if (value.swfs.length != size) return;

            var i:int = 0;

            for (; i < size; i++) {
                var asset:SlideAsset = new SlideAsset();
                asset.id = i;
                asset.assetURL = value.swfs[i];
                asset.thumbURL = value.thumbs[i];
                _assetsList.addItem(asset);
                _slidesList.addItem(new SlideItem(asset, this));
            }

            if (_slidesList.length > 0)
                slideItem = 0;

        }


        public function set slideItem(value:int):void {

            if (_slidesList.source.length < value + 1) return;

            _slideItem = _slidesList.getItemAt(value) as SlideItem;

            if (!_slideItem) return;

            clip = _slideItem.clip;
            _slideItem.load();

            //todo: preload

        }

        override public function resize(width:Number, height:Number):void {
            super.resize(width, height);
            if (_slideItem == null) return;
            const clip:UIComponent = _slideItem.clip;
            const r:Number = width / height;
            if (r > _slideItem.ratio) {
                clip.height = height;
                clip.width = clip.height * _slideItem.ratio;
            } else {
                clip.width = width;
                clip.height = clip.width / _slideItem.ratio;
            }
            clip.x = (width - clip.width) / 2;
            clip.y = (height - clip.height) / 2;
        }

        public function onLoaded():void {
            resize(this.width, this.height);
            if (!_inited) {
                _ratio = _slideItem.ratio;
                inited = true;
            }
        }

        override public function get ratio():Number {
            return _ratio;
        }

        override public function get useWB():Boolean {
            return true;
        }

        protected function set clip(value:UIComponent):void {
            // this method must override
        }

        override public function dispose():void {
            super.dispose();
            //TODO:clear slides array
        }
        ]]>
	</fx:Script>
	
</renderers:DefaultRenderer>
