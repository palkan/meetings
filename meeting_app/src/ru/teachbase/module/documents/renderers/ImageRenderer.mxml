<?xml version="1.0" encoding="utf-8"?>
<renderers:DefaultRenderer xmlns:fx="http://ns.adobe.com/mxml/2009"
                           xmlns:s="library://ns.adobe.com/flex/spark"
                           xmlns:renderers="ru.teachbase.module.documents.renderers.*"
                           width="100%" height="100%" clipAndEnableScrolling="true"
                           currentState="normal">
    <fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
        import mx.events.FlexEvent;

        import ru.teachbase.components.board.BoardCanvas;
        import ru.teachbase.module.documents.model.FileItem;
        import ru.teachbase.utils.shortcuts.translate;
        import ru.teachbase.utils.shortcuts.warning;

        private var imgLoader:Loader;
        [Bindable]
        private var _bitmap:Bitmap;
        private var padding:Number = 20;

        public var board:BoardCanvas;

        private var _initialHeight:Number = 0;

        override protected function completeHandler(event:FlexEvent):void {
            _initialWidth = 0;
            _useWB = true;
        }


        override public function resize(width:Number, height:Number):void {
            super.resize(width, height);
            if (isNaN(_ratio)) {
                return;
            }

            var thisRatio:Number = width / height;

            if (thisRatio > _ratio) {
                if (height <= _initialHeight)
                    imageContainer.height = height - padding * 2;
                else
                    imageContainer.height = _initialHeight - padding * 2;
                imageContainer.width = imageContainer.height * _ratio;
            } else {
                if (width <= _initialWidth)
                    imageContainer.width = width - padding * 2;
                else
                    imageContainer.width = _initialWidth - padding * 2;
                imageContainer.height = imageContainer.width / _ratio;
            }
            imageContainer.x = (width - imageContainer.width) / 2;
            imageContainer.y = (height - imageContainer.height) / 2;
        }

        override  public function set file(file:FileItem):void {

            super.file = file;

            if(busyIndicator) busyIndicator.visible = false;

            imgLoader = new Loader();

            var context:LoaderContext = new LoaderContext(true, ApplicationDomain.currentDomain);
            if (Security.sandboxType != Security.LOCAL_TRUSTED && Security.sandboxType != Security.APPLICATION) context.securityDomain = SecurityDomain.currentDomain;

            var req:URLRequest = new URLRequest(file.url);

            imgLoader.contentLoaderInfo.addEventListener(Event.COMPLETE, imageHandler);
            imgLoader.contentLoaderInfo.addEventListener(IOErrorEvent.IO_ERROR, function (e:*):void {

                warning("Can't load image: "+file.url+"; IO error");
                imgLoader = null;
                currentState = error.name;
            });
            imgLoader.addEventListener(SecurityErrorEvent.SECURITY_ERROR, function (e:*):void {

                warning("Can't load image: "+file.url+"; Security error");
                imgLoader = null;
                currentState = error.name;

            });

            try {
                imgLoader.load(req, context);
            } catch (evt:*) {
                warning("Unknow error loading image "+file.url+"");
                imgLoader = null;
                currentState = error.name;
            }
        }


        private function imageHandler(e:Event):void {
            busyIndicator.visible = false;
            var btm:Bitmap = imgLoader.content as Bitmap;
            if (!btm) {
                //          Logger.log('no bitmap in image', 'img');
                return;
            }
            _bitmap = btm;
            _ratio = btm.width / btm.height;

            _initialWidth = btm.width;
            _initialHeight = btm.height;

            inited = true;

            resize(this.width, this.height);
        }


        override public function get useWB():Boolean {
            return true;
        }

        ]]>
	</fx:Script>
    <renderers:states>
        <s:State id="normal" name="normal"/>
        <s:State id="error" name="error"/>
    </renderers:states>
	<s:BitmapImage source="{_bitmap}" id="imageContainer" smooth="true" includeIn="normal"/>
	<s:Group verticalCenter="0" horizontalCenter="0" id="busyIndicator" includeIn="normal">
		<s:BusyIndicator rotationInterval="50" symbolColor="0x71a7cd" width="60" height="60"/>
	</s:Group>
    <s:Group verticalCenter="0" horizontalCenter="0" id="errorCont" includeIn="error">
        <s:Label id="errorText" color="#990000" fontSize="12" height="25" initialize="errorText.text=translate('load_error');"/>
    </s:Group>

</renderers:DefaultRenderer>
